;; ███████╗ ██████╗  ██████╗ ██████╗   █████╗ ███████╗███╗   ███╗
;; ╚════██║██╔═████╗██╔═████╗╚════██╗ ██╔══██╗██╔════╝████╗ ████║
;;     ██╔╝██║██╔██║██║██╔██║ █████╔╝ ███████║███████╗██╔████╔██║
;;    ██╔╝ ████╔╝██║████╔╝██║██╔═══╝  ██╔══██║╚════██║██║╚██╔╝██║
;;    ██║  ╚██████╔╝╚██████╔╝███████╗ ██║  ██║███████║██║ ╚═╝ ██║
;;    ╚═╝   ╚═════╝  ╚═════╝ ╚══════╝ ╚═╝  ╚═╝╚══════╝╚═╝     ╚═╝
;;
;; This is an implementation of EIP-7002's pre-deploy contract. It implements an
;; unvalidated withdrawal requests queue for beacon chain validators. The queue
;; is tracked using head and tail index pointers. After the queue is emptied,
;; the pointers are reset to zero.
;;
;; Entrance to the queue is determined only by a call's ability to pay the
;; exponentially increasing fee. This fee is computed using a simple function
;; which approximates true exponential value. No verification of ownership is
;; done by the pre-deploy or the execution layer. Only once the requests are
;; being processed by the beacon chain is the validity verified. The fee is used
;; to avoid spamming of the withdrawal requests queue.

;; -----------------------------------------------------------------------------
;; CONSTANTS -------------------------------------------------------------------
;; -----------------------------------------------------------------------------

#define SYSTEM_ADDR 0xfffffffffffffffffffffffffffffffffffffffe

#define SLOT_EXCESS 0
#define SLOT_COUNT 1

#define QUEUE_HEAD 2
#define QUEUE_TAIL 3
#define QUEUE_OFFSET 4

#define MIN_FEE 1
#define TARGET_PER_BLOCK 2
#define MAX_PER_BLOCK 16
#define FEE_UPDATE_FRACTION 17
#define EXCESS_INHIBITOR 0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff

#define INPUT_SIZE 56   ;; the size of (pubkey ++ amount)
#define RECORD_SIZE 76  ;; the size of (address ++ pubkey ++ amount)
#define SLOTS_PER_ITEM 3 ;; (address, pubkey[0:32], pubkey[32:48] ++ amount)

;; ----------------------------------------------------------------------------
;; PROGRAM START --------------------------------------------------------------
;; ----------------------------------------------------------------------------

.start:
  ;; Protect the system subroutine by checking if the caller is the system
  ;; address.
  caller                ;; [caller]
  push SYSTEM_ADDR      ;; [sysaddr, caller]
  eq                    ;; [sysaddr == caller]
  jumpi @read_requests  ;; []

  ;; --------------------------------------------------------------------------
  ;; USER SUBROUTINE ----------------------------------------------------------
  ;; --------------------------------------------------------------------------
  ;;
  ;; Record new withdrawal request ~~
  ;; This is the default code path. It will attempt to record a user's request
  ;; so long as they pay the required fee.
  ;;
  ;; @audit ^ Comment off, there are still two code paths.

  ;; If calldatasize == 0, return the current excess requests.
  calldatasize          ;; [calldatasize]
  iszero                ;; [calldatasize == 0]
  jumpi @read_excess

  ;; Input data has the following layout:
  ;;
  ;;  +--------+--------+
  ;;  | pubkey | amount |
  ;;  +--------+--------+
  ;;      48       8

  ;; Verify the input is exactly 56 bytes.
  calldatasize          ;; [calldatasize]
  push INPUT_SIZE       ;; [INPUT_SIZE, calldatasize]
  eq                    ;; [INPUT_SIZE == calldatasize]
  iszero                ;; [INPUT_SIZE != calldatasize]
  jumpi @revert         ;; []

  ;; Compute the fee using fake expo and the current excess withdrawal requests.
  push FEE_UPDATE_FRACTION
  push SLOT_EXCESS      ;; [excess_slot, update_fraction]
  sload                 ;; [excess, update_fraction]

  ;; Check if the pre-fork inhibitor is still active, revert if so.
  dup1                  ;; [excess, excess, update_fraction]
  push EXCESS_INHIBITOR ;; [inhibitor, excess, excess, update_fraction]
  eq                    ;; [inhibitor == excess, excess, update_fraction]
  jumpi @revert         ;; [excess, update_fraction]

  push MIN_FEE          ;; [min_fee, excess, update_fraction]
  #include "../common/fake_expo.eas"                                            ;; @audit fake_expo leaves vals on stack

  ;; Determine if the fee provided is enough to cover the withdrawal request fee.
  callvalue             ;; [callvalue, req_fee]
  lt                    ;; [callvalue < req_fee]
  jumpi @revert         ;; []

  ;; The request can pay, increment withdrawal request count.
  push SLOT_COUNT
  sload                 ;; [req_count]
  push 1                ;; [1, req_count]
  add                   ;; [req_count+1]
  push SLOT_COUNT       ;; [slot, req_count+1]
  sstore                ;; []

  ;; Now insert request into queue. First, compute the base storage slot
  push QUEUE_TAIL       ;; [tail_idx_slot]
  sload                 ;; [tail_idx]
  dup1                  ;; [tail_idx, tail_idx]
  push SLOTS_PER_ITEM   ;; [slots, tail_idx, tail_idx]
  mul                   ;; [slots*tail_idx, tail_idx]
  push QUEUE_OFFSET     ;; [ost, 3*tail_idx, tail_idx]
  add                   ;; [slot, tail_idx]

  ;; Write address to queue.
  caller                ;; [caller, slot, ..]
  dup2                  ;; [slot, caller, slot, ..]
  sstore                ;; [slot, ..]

  push 1                ;; [1, slot, ..]
  add                   ;; [slot, ..]

  ;; Store pk[0:32] to queue.
  push 0                ;; [0, slot, ..]
  calldataload          ;; [pk[0:32], slot, ..]
  dup2                  ;; [slot, pk[0:32], slot, ..]
  sstore                ;; [slot, ..]

  push 1                ;; [1, slot, ..]
  add                   ;; [slot, ..]

  ;; Store pk2_am to queue.
  push 32               ;; [32, slot, ..]
  calldataload          ;; [pk2_am, slot, ..]
  swap1                 ;; [slot, pk2_am, ..]
  sstore                ;; [..]

  ;; Assemble log data.
  caller                ;; [caller, ..]
  push 96               ;; [96, caller, ..]
  shl                   ;; [caller, ..]
  push 0                ;; [0, caller, ..]
  mstore                ;; [..]
  push INPUT_SIZE       ;; [size, ..]
  push 0                ;; [ost, size, ..]
  push 20               ;; [dest, ost, size, ..]
  calldatacopy          ;; [..]

  ;; Log record.
  push RECORD_SIZE      ;; [size, ..]
  push 0                ;; [idx, size, ..]
  log0                  ;; [..]

  ;; Increment queue tail over last and write to storage.
  push 1                ;; [1, tail_idx]
  add                   ;; [tail_idx+1]
  push QUEUE_TAIL       ;; [tail_idx_slot]
  sstore                ;; []

  stop

read_excess:
  ;; This is the read path, where we return the current excess.
  ;; Reject any callvalue here to prevent lost funds.
  callvalue             ;; [value]
  iszero                ;; [value == 0]                                         @audit Remove double iszero (-6 gas)
  iszero                ;; [value != 0]
  jumpi @revert

  ;; Load excess withdrawal requests and return the value.
  push SLOT_EXCESS      ;; [excess_reqs_slot]
  sload                 ;; [excess_reqs]
  push 0                ;; [0, excess_reqs]
  mstore                ;; []
  push 32               ;; [32]                                                 @audit Use msize (-1 gas)
  push 0                ;; [0, 32]
  return                ;; []

;; ----------------------------------------------------------------------------
;; SYSTEM SUBROUTINE ----------------------------------------------------------
;; ----------------------------------------------------------------------------
;;
;; Pop withdrawal request from queue, update fee accumulator ~~
;; This is the logic executed by the protocol each block. It reads as many
;; requests as available from the queue, until the max withdrawal request per
;; block is reached. The requests are returned as a contiguous array of bytes
;; with each record being exactly 76 bytes.
;;
;;  Withdrawal request record:
;;
;;  +------+--------+--------+
;;  | addr | pubkey | amount |
;;  +------+--------+--------+
;;     20      48        8
;;
;; Because the requests are stored across three storage slots, there is some
;; shuffling to align the data.
;;
;; After reading the withdrawal requests, they are removed from the queue by
;; modifying the queue's head index. The excess requests accumulator is updated
;; so that the new cost of requesting a withdrawal is reflected. Finally, the
;; request count is reset.
read_requests:
  ;; Determine the size of the queue by calculating tail - head.
  push QUEUE_TAIL       ;; [tail_idx_slot, head_idx, head_idx]                  @audit Stack comment, remove last 2
  sload                 ;; [tail_idx]
  push QUEUE_HEAD       ;; [head_idx_slot, tail_idx]
  sload                 ;; [head_idx, tail_idx]

  ;; Now compute the count.
  dup1                  ;; [head_idx, head_idx, tail_idx]
  dup3                  ;; [tail_idx, head_idx, head_idx, tail_idx]
  sub                   ;; [count, head_idx, tail_idx]

  ;; Determine if count is greater than the max withdrawal requests.
  dup1                  ;; [count, count, head_idx, tail_idx]
  push MAX_PER_BLOCK    ;; [reqs_per_block, count, count, head_idx, tail_idx]
  gt                    ;; [reqs_per_block > count, count, head_idx, tail_idx]
  jumpi @begin_loop     ;; [count, head_idx, tail_idx]

  ;; Discard count, use the max withdrawal requests per block.
  pop                   ;; [head_idx, tail_idx]
  push MAX_PER_BLOCK    ;; [count, head_idx, tail_idx]

begin_loop:
  push 0                ;; [i, count, head_idx, tail_idx]

accum_loop:
  ;; This loop will read each request and byte bang it into a 76 byte chunk.

  ;; Bounds check, ensure i < count.
  dup2                  ;; [count, i, count, head_idx, tail_idx]
  dup2                  ;; [i, count, i, count, head_idx, tail_idx]
  eq                    ;; [i == count, i, count, head_idx, tail_idx]
  jumpi @update_head    ;; [i, count, head_idx, tail_idx]

  ;; Determine the storage slot of the address for this iteration. This value is
  ;; also the base for the other two storage slots containing the public key and
  ;; amount. The base slot will be (queue_offset + queue_head*3 + i*3).
  dup3                  ;; [head_idx, i, count, head_idx, ..]
  dup2                  ;; [i, head_idx, i, ..]
  add                   ;; [i+head_idx, i, ..]
  push 3                ;; [3, i+head_idx, i, ..]
  mul                   ;; [3*(i+head_idx), i, ..]
  push QUEUE_OFFSET     ;; [offset, 3*(i+head_idx), i, ..]
  add                   ;; [addr_offset, i, ..]

  ;; Read address.
  dup1                  ;; [addr_offset, addr_offset, i, ..]
  sload                 ;; [addr, addr_offset, i, ..]

  ;; Compute pk1 offset and read it.
  swap1                 ;; [addr_offset, addr, i, ..]
  push 1                ;; [1, addr_offset, addr, i, ..]
  add                   ;; [pk1_offset, addr, i, ..]
  dup1                  ;; [pk1_offset, pk1_offset, addr, i, ..]
  sload                 ;; [pk1, pk1_offset, addr, i, ..]

  ;; Compute pk2_am offset and read it.
  swap1                 ;; [pk1_offset, pk1, addr, i, ..]
  push 1                ;; [1, pk1_offset, pk1, addr, i, ..]
  add                   ;; [pk2_am_offset, pk1, addr, i, ..]
  sload                 ;; [pk2_am, pk1, addr, i, ..]

  ;; Reorder values.
  swap2                 ;; [addr, pk1, pk2_am, i, ..]

  ;; Write values to memory flat and contiguously. This require combining the
  ;; three storage elements (addr, pk1, pk2_am) so there is no padding.
  ;;
  ;; Each stack element has the following layout:
  ;;
  ;; A: addr
  ;;  0x00 | 00 00 00 00 00 00 00 00 00 00 00 00 aa aa aa aa
  ;;  0x10 | aa aa aa aa aa aa aa aa aa aa aa aa aa aa aa aa
  ;;
  ;; B: pk[0:32] -> pk1
  ;;  0x00 | bb bb bb bb bb bb bb bb bb bb bb bb bb bb bb bb
  ;;  0x10 | bb bb bb bb bb bb bb bb bb bb bb bb bb bb bb bb
  ;;
  ;; C: pk[32:48] ++ am[0:8] -> pk2_am
  ;;  0x00 | cc cc cc cc cc cc cc cc cc cc cc cc cc cc cc cc
  ;;  0x10 | dd dd dd dd dd dd dd dd 00 00 00 00 00 00 00 00
  ;;
  ;; To get these three stack elements into the correct contiguous format, it is
  ;; neccessary to combine them in the follow form:
  ;;
  ;;  (A[12:32] ++ B[0:12], B[12:32] ++ C[0:12], C[12:24])

  ;; Compute offset = i*RECORD_SIZE.
  dup4                  ;; [i, addr, pk1, pk2_am, i, ..]
  push RECORD_SIZE      ;; [size, i, addr, pk1, pk2_am, i, ..]
  mul                   ;; [offset, addr, pk1, pk2_am, i, ..]

  ;; Shift addr bytes.
  swap1                 ;; [addr, offset, pk1, pk2_am, i, ..]
  push 12*8             ;; [96, addr, offset, pk1, pk2_am, i, ..]
  shl                   ;; [addr<<96, offset, pk1, pk2_am, i, ..]

  ;; Store addr at current offset.
  dup2                  ;; [offset, addr<<96, offset, pk1, pk2_am, i, ..]
  mstore                ;; [offset, pk1, pk2_am, i, ..]
  push 20               ;; [20, offset, pk1, pk2_am, i, ..]
  add                   ;; [offset, pk1, pk2_am, i, ..]

  ;; Store pk1 at offset = i*RECORD_SIZE + 20.
  swap1                 ;; [pk1, offset, pk2_am, i, ..]
  dup2                  ;; [offset, pk1, offset, pk2_am, i, ..]
  mstore                ;; [offset, pk2_am, i, ..]
  push 32               ;; [32, offset, pk2_am, i, ..]
  add                   ;; [offset, pk2_am, i, ..]

  ;; Extract pk2 from pk2_am.
  dup2                  ;; [pk2_am, offset, pk2_am, i, ..]
  push pk2_mask         ;; [mask, pk2_am, offset, pk2_am, i, ..]
  and                   ;; [pk2, offset, pk2_am, i, ..]

  ;; Store pk2 at offset = i*RECORD_SIZE + 52.
  dup2                  ;; [offset, pk2, offset, pk2_am, i, ..]
  mstore                ;; [offset, pk2_am, i, ..]
  push 16               ;; [16, offset, pk2_am, i, ..]
  add                   ;; [offset, pk2_am, i, ..]

  ;; Extract am from pk2_am.
  swap1                 ;; [pk2_am, offset, i, ..]
  push 8*8              ;; [shft, pk2_am, offset, i, ..]
  shr                   ;; [am, offset, i, ..]

  ;; Store am at offset = i*RECORD_SIZE + 68.
  swap1                 ;; [offset, am, i, ..]
  %mstore_uint64_le()   ;; [i, ..]

  ;; Increment i.
  push 1                ;; [1, i, ..]
  add                   ;; [i+1, ..]

  jump @accum_loop      ;; [i, count, head_idx, tail_idx]

update_head:
  ;; All requests have been read, update queue by adding the count read to the
  ;; current head index.
  swap2                 ;; [head_idx, count, count, tail_idx]
  add                   ;; [new_head_idx, count, tail_idx]

  ;; If the new head is equal to the tail, reset the queue by zeroing them both.
  dup1                  ;; [new_head_idx, new_head_idx, count, tail_idx]
  swap3                 ;; [tail_idx, new_head_idx, count, new_head_idx]
  eq                    ;; [new_head_idx == tail_idx, count, new_head_idx]
  jumpi @reset_queue    ;; [count, new_head_idx]

  ;; Otherwise, write the new head to storage.
  swap1                 ;; [new_head_idx, count]
  push QUEUE_HEAD       ;; [head_idx_slot, new_head_idx, count]
  sstore                ;; [count]

  jump @update_excess   ;; [count]

reset_queue:
  ;; Since the queue is empty, both the head and tail indexes can be zeroed.
  swap1                 ;; [new_head_idx, count]
  pop                   ;; [count]

  push 0                ;; [0, count]
  push QUEUE_HEAD       ;; [head_slot, 0, count]
  sstore                ;; [count]

  push 0                ;; [0, count]
  push QUEUE_TAIL       ;; [tail_slot, 0, count]
  sstore                ;; [count]

update_excess:
  ;; Update the new excess withdrawal requests.
  push SLOT_EXCESS      ;; [excess_slot, count]
  sload                 ;; [excess, count]

  ;; Check if excess needs to be reset to 0 for first iteration after
  ;; activation.
  dup1                  ;; [excess, excess, count, count]
  push EXCESS_INHIBITOR ;; [inhibitor, excess, excess, count, count]
  eq                    ;; [inhibitor == excess, excess, count, count]
  iszero                ;; [inhibitor != excess, excess, count, count]
  jumpi @skip_reset     ;; [excess, count, count]

  ;; Drop the excess from storage and use 0.                                    @audit Comment of: storage<->stack
  pop                   ;; [count, count]
  push 0                ;; [reset_excess, count]

skip_reset:
  push SLOT_COUNT       ;; [count_slot, excess, count]
  sload                 ;; [count, excess, count]

  ;; If the sum of the previous excess requests and requests added in the
  ;; current block is greater than the target, subtract the target from the sum
  ;; and set it as the new excess withdrawal requests value.
  push TARGET_PER_BLOCK ;; [target, count, excess, count]
  dup3                  ;; [excess, target, count, excess]
  dup3                  ;; [count, excess, target, count, excess, count]
  add                   ;; [count+excess, target, count, excess, count]
  ;; @audit If count >= 15 while inhibitor still set, contract is broken!
  gt                    ;; [count+excess > target, count, excess, count]
  jumpi @compute_excess ;; [count, excess, count]

  ;; Zero out excess.
  pop                   ;; [excess, count]
  pop                   ;; [count]
  push 0                ;; [0, count]
  jump @store_excess    ;; [0, count]

compute_excess:
  add                   ;; [count+excess, count]
  push TARGET_PER_BLOCK ;; [target, count+excess, count]
  swap1                 ;; [count+excess, target, count]
  sub                   ;; [new_excess, count]

store_excess:
  push SLOT_EXCESS      ;; [excess_slot, new_excess, count]
  sstore                ;; [count]

  ;; Reset withdrawal request count.
  push 0                ;; [0, count]
  push SLOT_COUNT       ;; [count_slot, 0, count]
  sstore                ;; [count]

  ;; Return the withdrawal requests.
  push RECORD_SIZE      ;; [record_size, count]
  mul                   ;; [size]
  push 0                ;; [0, size]
  return                ;; []

;; Revert subroutine.
revert:
  push 0
  push 0
  revert

;; ----------------------------------------------------------------------------
;; MACROS ---------------------------------------------------------------------
;; ----------------------------------------------------------------------------

;; This defines a mask for accessing the top 16 bytes of a number.
#define pk2_mask 0xffffffffffffffffffffffffffffffff00000000000000000000000000000000

;; Helper for storing little-endian amount.
#define %mstore_uint64_le() { ;; [offset, value]
;; ~~ Explanation ~~
;;
;; amt  = 0x0011223344556677
;; want = 0x7766554433221100
;;
;; Note that amt is normally stored in memory as:
;;
;;   [0x00:0x20]: 0x0000000000000000000000000000000000000000000000000011223344556677
;;                  ^^ hb                                                         ^^ lb
;;
;; This means the highest order byte is at 0, lowest order byte at 31:
;;
;;   hb := byte(0, mload(0))
;;   lb := byte(31, mload(0))
;;   assert(hb == 0x00)
;;   assert(lb == 0x77)
;;
;; Therefore, we want little endian amt in memory as:
;;
;;   [0x00:0x20]: 0x7766554433221100000000000000000000000000000000000000000000000000
;;                  ^^ lb                                                         ^^ hb
;;
;; ie:
;;
;;   hb := byte(31, mload(0))
;;   lb := byte(0, mload(0))
;;   assert(hb == 0x77)
;;   assert(lb == 0x00)
;;
;; Note that hb/lb names are wrong now. hb is the lowest significant byte and lb the highest.
;;
;; ~~ How is this achieved?
;;
;; The alg is as follows:
;; First note that its an unrolled loop, ie each code block does the same thing.
;;
;; Take the value and shift it to the far right, ie `value >> (7-i)*8` (7 = len - 1), eg:
;; 0x0011223344556677 >> (7-1)*8 = 0x0000000000000011
;;     ^^ second iteration, i = 1
;;
;; For storing to memory mstore8 is used, ie we store single bytes.
;; We first store at the right most slot `offset + 7`:
;;
;;   [0x00:0x08]: 0x00000000000077
;;                              ^^ = offset + 7
;;
;; Next, we store the second byte at the second last slot:
;;
;;   [0x00:0x08]: 0x00000000006677
;;                            ^^ = offset + 6
;;
;; and so on...
  dup2                  ;; [value, offset, value]
  push 7*8              ;; [56, value, offset, value]
  shr                   ;; [value>>56, offset, value]
  dup2                  ;; [offset, value>>56, offset, value]
  push 7                ;; [7, offset, value>>56, offset, value]
  add                   ;; [offset+7, value>>56, offset, value]
  mstore8               ;; [offset, value]

  dup2                  ;; [value, offset, value]
  push 6*8              ;; [48, value, offset, value]
  shr                   ;; [value>>48, offset, value]
  dup2                  ;; [offset, value>>48, offset, value]
  push 6                ;; [6, offset, value>>48, offset, value]
  add                   ;; [offset+6, value>>48, offset, value]
  mstore8               ;; [offset, value]

  dup2                  ;; [value, offset, value]
  push 5*8              ;; [40, value, offset, value]
  shr                   ;; [value>>40, offset, value]
  dup2                  ;; [offset, value>>40, offset, value]
  push 5                ;; [2, offset, value>>40, offset, value]
  add                   ;; [offset+5, value>>40, offset, value]
  mstore8               ;; [offset, value]

  dup2                  ;; [value, offset, value]
  push 4*8              ;; [32, value, offset, value]
  shr                   ;; [value>>32, offset, value]
  dup2                  ;; [offset, value>>32, offset, value]
  push 4                ;; [4, offset, value>>32, offset, value]
  add                   ;; [offset+4, value>>32, offset, value]
  mstore8               ;; [offset, value]

  dup2                  ;; [value, offset, value]
  push 3*8              ;; [24, value, offset, value]
  shr                   ;; [value>>24, offset, value]
  dup2                  ;; [offset, value>>24, offset, value]
  push 3                ;; [3, offset, value>>24, offset, value]
  add                   ;; [offset+3, value>>24, offset, value]
  mstore8               ;; [offset, value]

  dup2                  ;; [value, offset, value]
  push 2*8              ;; [16, value, offset, value]
  shr                   ;; [value>>16, offset, value]
  dup2                  ;; [offset, value>>16, offset, value]
  push 2                ;; [2, offset, value>>16, offset, value]
  add                   ;; [offset+2, value>>16, offset, value]
  mstore8               ;; [offset, value]

  dup2                  ;; [value, offset, value]
  push 1*8              ;; [8, value, offset, value]
  shr                   ;; [value>>8, offset, value]
  dup2                  ;; [offset, value>>8, offset, value]
  push 1                ;; [1, offset, value>>8, offset, value]
  add                   ;; [offset+1, value>>8, offset, value]
  mstore8               ;; [offset, value]

  mstore8               ;; []
}
